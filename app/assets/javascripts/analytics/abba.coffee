# This file modified from source original generated by Stripe, Inc. 
# Below is the original license.
#
# The MIT License
#
# Copyright (c) 2013- Stripe, Inc. (https://stripe.com)
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
# THE SOFTWARE.

scribble.analytics or= {}
scribble.analytics.helpers or= {}

request = (params = {}, callback) ->
  scribble.analytics.helpers.events.trackGoogleEvent(
    params.experiment, 
    params.variant,
    params.label
  )

  true

setCookie = (name, value, options = {}) ->
  if options.expires is true
    options.expires = -1

  if typeof options.expires is 'number'
    expires = new Date
    expires.setTime(expires.getTime() + options.expires*24*60*60*1000)
    options.expires = expires

  value  = (value + '').replace(/[^!#-+\--:<-\[\]-~]/g, encodeURIComponent)
  cookie = encodeURIComponent(name) + '=' + value

  cookie += ';expires=' + options.expires.toGMTString() if options.expires
  cookie += ';path=' + options.path if options.path
  cookie += ';domain=' + options.domain if options.domain
  document.cookie = cookie

getCookie = (name) ->
  cookies = document.cookie.split('; ')
  for cookie in cookies
    index = cookie.indexOf('=')
    key   = decodeURIComponent(cookie.substr(0, index))
    value = decodeURIComponent(cookie.substr(index + 1))
    return value if key is name
  null

extend = (target, args...) ->
  for source in args
    for key, value of source when value?
      target[key] = value
  return target

# Public

class @Abba
  endpoint: 'http://localhost:4567'
  defaults:
    path: '/'
    expires: 600

  constructor: (name, options = {}) ->
    unless name
      throw new Error('Experiment name required')

    # Force constructor
    if this not instanceof Abba
      return new Abba(name, options)

    @name     = name
    @options  = options
    @variants = []

    @endpoint = @options.endpoint or @constructor.endpoint

  variant: (name, options, callback) ->
    if typeof name isnt 'string'
      throw new Error('Variant name required')

    if typeof options isnt 'object'
      callback = options
      options  = {}

    options.name     = name
    options.callback = callback
    options.weight  ?= 1

    @variants.push(options)
    this

  control: (name = 'Control', options, callback) ->
    if typeof options isnt 'object'
      callback = options
      options  = {}

    options.control = true
    @variant(name, options, callback)

  continue: ->
    # Use the same variant as before, don't record anything
    if variant = @getPreviousVariant()
      @useVariant(variant)
    this

  start: (name, options = {}) ->
    if variant = @getPreviousVariant()
      # Use the same variant as before, don't record anything
      @useVariant(variant)
      return this

    if name?
      # Custom variant provided
      variant = @getVariantForName(name)
      variant or=
        name:    name
        control: options.control

    else
      # Or choose a weighted random variant
      totalWeight   = 0
      totalWeight  += v.weight for v in @variants
      randomWeight  = Math.random() * totalWeight
      variantWeight = 0

      for variant in @variants
        variantWeight += variant.weight
        break if variantWeight >= randomWeight

    throw new Error('No variants added') unless variant
    @recordStart(variant)
    @useVariant(variant)
    this

  complete: (name, options = {}) ->
    # Optionally pass a name, or read from the cookie
    name or= @getVariantCookie()
    return this unless name

    # If the test has already been completed, return
    return this if @hasPersistCompleteCookie()

    # Persist forever or reset test
    if @options.persist
      @setPersistCompleteCookie()
    else
      @reset()

    @recordComplete(name, options.label)
    this

  reset: ->
    @removeVariantCookie()
    @removePersistCompleteCookie()
    @result = null
    this

  # Private

  getVariantForName: (name) ->
    (v for v in @variants when v.name is name)[0]

  useVariant: (variant) ->
    variant?.callback?()
    @chosen = variant

  recordStart: (variant) ->
    # Record which experiment was run
    request(
      experiment: @name,
      variant:    variant.name,
      label: 'start'
    )

    # Set the variant we chose as a cookie
    @setVariantCookie(variant.name)

  recordComplete: (name, label = 'complete') ->
    # Record the experiment was completed
    request(experiment: @name, variant: name, label: label)

  # Variant Cookie

  getPreviousVariant: ->
    if name = @getVariantCookie()
      @getVariantForName(name)

  getVariantCookie: ->
    @getCookie("abbaVariant_#{@name}")

  setVariantCookie: (value) ->
    @setCookie("abbaVariant_#{@name}", value, expires: @options.expires)

  removeVariantCookie: ->
    @setCookie("abbaVariant_#{@name}", '', expires: true)

  # Complete Cookie

  setPersistCompleteCookie: ->
    @setCookie("abbaPersistComplete_#{@name}", '1', expires: @options.expires)

  hasPersistCompleteCookie: ->
    !!@getCookie("abbaPersistComplete_#{@name}")

  removePersistCompleteCookie: ->
    @setCookie("abbaPersistComplete_#{@name}", '', expires: true)

  setCookie: (name, value, options = {}) ->
    setCookie(name, value, extend({}, @defaults, options))

  getCookie: (name, options = {}) ->
    getCookie(name, extend({}, @defaults, options))
